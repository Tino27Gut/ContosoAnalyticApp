Set vQvd_Nombre = L2_dimcustomer;


/* Mapping */

Tbl_Ref_MaritalStatus:
Mapping
Load * Inline [
MaritalStatusId, MaritalStatus
M, Casado (a)
S, Soltero (a)
D, Divorciado (a)
V, Viudo (a)
];

/*

Casados con hijos
Casados sin hijos
Solteros con hijos
Solteros sin hijos

Tipo de cliente empresa = NA

*/



$(vQvd_Nombre):
	LOAD *,
    
    	IF(CustomerType = 'Person',
        	IF(MaritalStatusId = 'M' AND TotalChildren > 0,
            	'Casado con hijos',
            	IF(MaritalStatusId = 'M' AND TotalChildren = 0,
                	'Casado sin hijos',
                	IF(MaritalStatusId = 'S' AND TotalChildren > 0,
                    	'Soltero con hijos',
                    	IF(MaritalStatusId = 'S' AND TotalChildren = 0,
                        	'Soltero sin hijos',
                            'NA'
               )))), 'NA') AS CustomerProfile
        
        
    ;
    LOAD
        CustomerKey,
        GeographyKey as GeographyKey_Customer,
        CustomerLabel,
        Title,
        FirstName &' '& if(not IsNull(MiddleName), MiddleName &' ') & LastName as FullName,
        NameStyle,
        BirthDate,
        MaritalStatus as MaritalStatusId,
        ApplyMap('Tbl_Ref_MaritalStatus', MaritalStatus, 'Empresa') as MaritalStatus,
        Suffix,
        Gender,
        EmailAddress,
        YearlyIncome,
        TotalChildren,
        NumberChildrenAtHome,
        Education,
        Occupation,
        HouseOwnerFlag,
        NumberCarsOwned,
        AddressLine1 as AddressLine1_Customer,
        AddressLine2 as AddressLine2_Customer,
        Phone,
        DateFirstPurchase,
        CustomerType,
        CompanyName
    FROM [$(vR_QvdExt)L1_MYSQL_dimcustomer.qvd]
    (qvd);
Left Join
    LOAD
        GeographyKey as GeographyKey_Customer,
        GeographyTypeKey,
        GeographyType,
        ContinentName as ContinentName_Customer,
        CityName as CityName_Customer,
        StateProvinceName as StateProvinceName_Customer,
        RegionCountryName as RegionCountryName_Customer,
        Geometry
    FROM [$(vR_QvdExt)L1_MYSQL_dimgeography.qvd]
    (qvd);
Left Join
    LOAD
        SalesTerritoryKey,
        GeographyKey as GeographyKey_Customer,
        SalesTerritoryLabel,
        SalesTerritoryName,
        SalesTerritoryRegion,
        SalesTerritoryCountry,
        SalesTerritoryGroup,
        SalesTerritoryLevel,
        SalesTerritoryManager,
        StartDate,
        EndDate,
        Status as Status_SalesTerritory
    FROM [$(vR_QvdExt)L1_MYSQL_dimsalesterritory.qvd]
    (qvd);
Left Join
    LOAD
        CustomerKey,
        "TAG" AS RFM,
        ACTIVITY AS ACTIVITY_RFM,
        ACTIONABLE_TIP AS ACTIONABLE_TIP_RFM,
        Transac AS TransactionByCustomer
    FROM [$(vR_QvdExt)L1_MYSQL_dimRFM.qvd]
    (qvd);

Store $(vQvd_Nombre) into [$(vR_QvdTra)$(vQvd_Nombre).qvd] (qvd);
Drop Table $(vQvd_Nombre);